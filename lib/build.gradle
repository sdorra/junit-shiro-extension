/*
 * The MIT License
 *
 * Copyright (c) 2021, Sebastian Sdorra
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

plugins {
  id 'java-library'
  id 'signing'
  id 'maven-publish'
}

repositories {
  jcenter()
}

dependencies {
  api 'org.apache.shiro:shiro-core:1.7.1'
  api 'org.junit.jupiter:junit-jupiter-api:5.6.2'
  api 'org.junit.jupiter:junit-jupiter-engine:5.6.2'

  testImplementation 'org.assertj:assertj-core:3.19.0'
}

tasks.named('test') {
  // Use junit platform for unit tests.
  useJUnitPlatform()
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from(components.java)
      artifactId = 'junit-shiro-extension'
      pom {
        name = 'JUnit Shiro Extension'
        description = 'Simplify unit tests with JUnit 5 and Apache Shiro.'
        url = 'https://github.com/sdorra/junit-shiro-extension'
        inceptionYear = '2021'
        licenses {
          license {
            name = 'The MIT license'
            url = 'https://www.opensource.org/licenses/mit-license.php'
          }
        }
        developers {
          developer {
            id = 'sdorra'
            name = 'Sebastian Sdorra'
            email = 's.sdorra@gmail.com'
            timezone = 'Europe/Berlin'
          }
        }
        scm {
          url = 'https://github.com/sdorra/junit-shiro-extension'
          connection = 'scm:git:git://github.com/sdorra/junit-shiro-extension.git'
          developerConnection = 'scm:git:git://github.com/sdorra/junit-shiro-extension.git'
        }
        issueManagement {
          system = "github"
          url = 'https://github.com/sdorra/junit-shiro-extension/issues'
        }
      }
    }
  }
}

signing {
  useGpgCmd()
  sign publishing.publications
}

nexusPublishing {
  repositories {
    sonatype()
  }
}
